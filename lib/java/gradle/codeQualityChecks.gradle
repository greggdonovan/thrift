// =================================================================
// Configure the Gradle code quality plugins here.
//

dependencies {
    spotbugs configurations.spotbugsPlugins.dependencies
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'
    errorprone "com.google.errorprone:error_prone_core:${property('errorprone.version')}"
    errorprone "com.uber.nullaway:nullaway:${nullawayVersion}"
}

// ErrorProne configuration - validates both library and generated code
tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        // Enable checking of generated code to ensure Thrift compiler output is clean
        disableWarningsInGeneratedCode = false
        // Disable specific checks that are too noisy for generated code
        disable("MissingSummary")  // Generated code doesn't need javadoc summaries

        // NullAway configuration for null safety analysis
        option("NullAway:AnnotatedPackages", "org.apache.thrift")
        option("NullAway:JSpecifyMode", "true")
    }
}

// see https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugsextension
// Note: String values trigger deprecation warning in Gradle 9, will need enum values for Gradle 10
spotbugs {
    ignoreFailures = true
    toolVersion = '4.9.8'
    effort = 'max'
    reportLevel = 'low'
    excludeFilter = file('code_quality_tools/findbugs-filter.xml')
}

// see https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugstask
spotbugsMain {
    reports {
        text.required = false
        html.required = true
        xml.required = false
    }
}

pmd {
    ignoreFailures = true
    toolVersion = '7.9.0'
    sourceSets = [ sourceSets.main ]
    // PMD 7 uses category-based rulesets
    ruleSets = [ 'category/java/errorprone.xml' ]
}

tasks.withType(Pmd) {
    reports {
        html.required = true
        xml.required = false
    }
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat('1.28.0')
    }
}
