
// =================================================================
// Configure the Gradle code quality plugins here.
//

dependencies {
    spotbugs configurations.spotbugsPlugins.dependencies
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0'
    errorprone "com.google.errorprone:error_prone_core:${property('errorprone.version')}"
}

// ErrorProne configuration - validates both library and generated code
tasks.withType(JavaCompile).configureEach {
    options.errorprone {
        // Enable checking of generated code to ensure Thrift compiler output is clean
        disableWarningsInGeneratedCode = false
        // Disable specific checks that are too noisy for generated code
        disable("MissingSummary")  // Generated code doesn't need javadoc summaries
    }
}

// see https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugsextension
spotbugs {
    ignoreFailures = true
    toolVersion = '4.5.3'
    effort = 'max'
    reportLevel = 'low'
    excludeFilter = file('code_quality_tools/findbugs-filter.xml')
}

// see https://spotbugs-gradle-plugin.netlify.app/com/github/spotbugs/snom/spotbugstask
spotbugsMain {
    reports {
        text.enabled = false
        html.enabled = true
        xml.enabled = false
    }
}

pmd {
    ignoreFailures = true
    toolVersion = '6.0.0'
    sourceSets = [ sourceSets.main ]
    ruleSets = [ 'java-basic' ]
}

tasks.withType(Pmd) {
    reports {
        html.required = true
        xml.required = false
    }
}

spotless {
    java {
        target 'src/**/*.java'
        googleJavaFormat()
    }
}
