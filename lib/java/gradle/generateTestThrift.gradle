/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements. See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership. The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License. You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

// Following Gradle best practices to keep build logic organized

import javax.inject.Inject
import org.gradle.api.tasks.Internal
import org.gradle.process.ExecOperations

// Generated code locations for Unit tests
ext.genSrc = file("$buildDir/gen-java")
ext.genBeanSrc = file("$buildDir/gen-javabean")
ext.genReuseSrc = file("$buildDir/gen-javareuse")
ext.genFullCamelSrc = file("$buildDir/gen-fullcamel")
ext.genOptionTypeSrc = file("$buildDir/gen-option-type")
ext.genUnsafeSrc = file("$buildDir/gen-unsafe")
ext.genDefinitionOrderTestASrc = file("$buildDir/resources/test/definition-order-test/a")
ext.genDefinitionOrderTestBSrc = file("$buildDir/resources/test/definition-order-test/b")

// Add the generated code directories to the test source set
// Exclude package-info.java from secondary gen directories to avoid duplicate package annotation errors
sourceSets {
    test.java.srcDirs genSrc, genBeanSrc, genReuseSrc, genFullCamelSrc, genOptionTypeSrc, genUnsafeSrc
    test.java.filter.exclude { element ->
        // Exclude package-info.java from non-primary generated directories to avoid conflicts
        def file = element.file
        def isPackageInfo = file.name == 'package-info.java'
        // Normalize separators so this works on Unix and Windows paths.
        def normalizedPath = file.path.replace('\\', '/')
        def isSecondaryGenDir = normalizedPath.contains('/gen-javabean/') ||
                                normalizedPath.contains('/gen-javareuse/') ||
                                normalizedPath.contains('/gen-fullcamel/') ||
                                normalizedPath.contains('/gen-option-type/') ||
                                normalizedPath.contains('/gen-unsafe/')
        return isPackageInfo && isSecondaryGenDir
    }
}

// ----------------------------------------------------------------------------
// Code generation for Unit Testing

// Abstract task class that can inject ExecOperations for Gradle 9.x compatibility
abstract class ThriftGeneratorTask extends DefaultTask {
    @Inject
    abstract ExecOperations getExecOperations()

    @Internal
    List<Map<String, Object>> thriftCompilations = []
    @Internal
    ByteArrayOutputStream outputBuffer = new ByteArrayOutputStream()

    void addCompilation(File thriftFile, String generator, File outputDir) {
        thriftCompilations << [thriftFile: thriftFile, generator: generator, outputDir: outputDir]
        inputs.file thriftFile
        outputs.dir outputDir
    }

    @TaskAction
    void generate() {
        thriftCompilations.each { compilation ->
            compilation.outputDir.mkdirs()
            def result = execOperations.exec {
                executable project.file(project.thriftCompiler)
                args '--gen', compilation.generator
                args '-out', compilation.outputDir
                args compilation.thriftFile
                standardOutput = outputBuffer
                errorOutput = outputBuffer
                ignoreExitValue = true
            }
            if (result.exitValue != 0) {
                println outputBuffer.toString()
                throw new GradleException("Thrift compiler failed with exit code ${result.exitValue}")
            }
        }
    }
}

// Helper to find thrift file
ext.findThriftFile = { String thriftFileName ->
    def thriftFile = file("$thriftRoot/test/$thriftFileName")
    if (!thriftFile.exists()) {
        thriftFile = file("$projectDir/src/test/resources/$thriftFileName")
        assert thriftFile.exists(), "can't find $thriftFile"
    }
    return thriftFile
}

def generateTask = tasks.register('generate') {
    group = 'Build'
    description = 'Generate all unit test Thrift sources'
}

def generateJavaTask = tasks.register('generateJava', ThriftGeneratorTask) {
    description = 'Generate the thrift gen-java source'

    addCompilation(findThriftFile('ThriftTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('JavaTypes.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('DebugProtoTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('DoubleConstantsTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('OptionalRequiredTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('ManyOptionals.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('JavaDeepCopyTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('EnumContainersTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('JavaBinaryDefault.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('VoidMethExceptionsTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('JavaAnnotationTest.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('partial/thrift_test_schema.thrift'), 'java', genSrc)
    addCompilation(findThriftFile('FuzzTest.thrift'), 'java', genSrc)
}

def generateOptionTypeJavaTask = tasks.register('generateOptionTypeJava', ThriftGeneratorTask) {
    description = 'Generate the thrift gen-option-type source'

    addCompilation(findThriftFile('JavaOptionTypeOptionalTest.thrift'), 'java:option_type=jdk8', genOptionTypeSrc)
}

def generateBeanJavaTask = tasks.register('generateBeanJava', ThriftGeneratorTask) {
    description = 'Generate the thrift gen-javabean source'

    addCompilation(findThriftFile('JavaBeansTest.thrift'), 'java:beans,nocamel,future_iface', genBeanSrc)
}

def generateReuseJavaTask = tasks.register('generateReuseJava', ThriftGeneratorTask) {
    description = 'Generate the thrift gen-javareuse source'

    addCompilation(findThriftFile('FullCamelTest.thrift'), 'java:fullcamel,future_iface', genFullCamelSrc)
}

def generateFullCamelJavaTask = tasks.register('generateFullCamelJava', ThriftGeneratorTask) {
    description = 'Generate the thrift gen-fullcamel source'

    addCompilation(findThriftFile('ReuseObjects.thrift'), 'java:reuse_objects', genReuseSrc)
}

def generateUnsafeBinariesJavaTask = tasks.register('generateUnsafeBinariesJava', ThriftGeneratorTask) {
    description = 'Generate the thrift gen-unsafebinaries source'

    addCompilation(findThriftFile('UnsafeTypes.thrift'), 'java:unsafe_binaries', genUnsafeSrc)
}

def generateWithAnnotationMetadataTask = tasks.register('generateWithAnnotationMetadata', ThriftGeneratorTask) {
    description = 'Generate with annotation enabled and add to the default source'

    addCompilation(findThriftFile('JavaAnnotationTest.thrift'), 'java:annotations_as_metadata', genSrc)
}

def generateJavaDefinitionOrderTestJavaTask = tasks.register('generateJavaDefinitionOrderTestJava', ThriftGeneratorTask) {
    description = 'Generate fields defined in different order and add to build dir so we can compare them'

    addCompilation(findThriftFile('JavaDefinitionOrderA.thrift'), 'java', genDefinitionOrderTestASrc)
    addCompilation(findThriftFile('JavaDefinitionOrderB.thrift'), 'java', genDefinitionOrderTestBSrc)
}

tasks.named('compileTestJava') {
    dependsOn generateTask
}

tasks.named('generate') {
    dependsOn generateJavaTask
    dependsOn generateOptionTypeJavaTask
    dependsOn generateBeanJavaTask
    dependsOn generateReuseJavaTask
    dependsOn generateFullCamelJavaTask
    dependsOn generateUnsafeBinariesJavaTask
    dependsOn generateWithAnnotationMetadataTask
    dependsOn generateJavaDefinitionOrderTestJavaTask
}
